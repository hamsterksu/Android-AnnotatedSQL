subprojects {

	sourceCompatibility = 1.6
	group = 'com.github.hamsterksu'
	version = '1.9.0'

    apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'signing'

    repositories {
       mavenCentral()
    }

	sourceSets {
		main {
			java {
				srcDir 'src'
			}
			resources {
				srcDir 'res'
			}
		}
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = 'javadoc'
		from 'build/docs/javadoc'
	}
 
	task sourcesJar(type: Jar) {
		from sourceSets.main.allSource
		classifier = 'sources'
	}
	
	artifacts {
		archives jar
		archives javadocJar
		archives sourcesJar
	}
	
	signing {
		sign configurations.archives
	}

    jar {
        manifest.attributes provider: 'gradle'
    }
	
	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
	 
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: sonatypeUsername, password: sonatypePassword)
				}
	 
				pom.project {
				   name 'android-annotatedsql'
				   packaging 'jar'
				   description 'Android library to create schema and content provider by using annotations'
				   url 'https://github.com/hamsterksu/Android-AnnotatedSQL'
	 
				   scm {
					   url 'scm:git@github.com:hamsterksu/Android-AnnotatedSQL.git'
					   connection 'scm:git@github.com:hamsterksu/Android-AnnotatedSQL.git'
					   developerConnection 'scm:git@github.com:hamsterksu/Android-AnnotatedSQL.git'
				   }
	 
				   licenses {
					   license {
						   name 'The Apache Software License, Version 2.0'
						   url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						   distribution 'repo'
					   }
				   }
	 
				   developers {
					   developer {
						   id 'hamsterksu'
						   name 'Gennadiy Dubina'
					   }
					   
					   developer {
						   id 'TheHiddenDuck'
						   name ' Alexander Osmanov'
					   }
				   }
			   }
			}
		}
	}
}